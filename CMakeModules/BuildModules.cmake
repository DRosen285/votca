include(ExternalProject)
set(ENABLED_VOTCA_PACKAGES tools csg csg-tutorials)
if(BUILD_CSG_MANUAL)
  list(APPEND ENABLED_VOTCA_PACKAGES csg-manual)
endif()
if(BUILD_CSGAPPS)
  list(APPEND ENABLED_VOTCA_PACKAGES csgapps)
endif()
if(BUILD_CTP)
  list(APPEND ENABLED_VOTCA_PACKAGES ctp)
endif()
if(BUILD_XTP)
  list(APPEND ENABLED_VOTCA_PACKAGES xtp)
endif()

foreach(VOTCA_MODULE ${ENABLED_VOTCA_PACKAGES})
  set(VOTCA_DEPENDS)
  if(VOTCA_MODULE STREQUAL "csg")
    set(VOTCA_DEPENDS tools)
  elseif((VOTCA_MODULE STREQUAL "csgapps") OR (VOTCA_MODULE STREQUAL "csg-tutorials") OR (VOTCA_MODULE STREQUAL "csg-manual") OR (VOTCA_MODULE STREQUAL "ctp"))
    set(VOTCA_DEPENDS csg)
  elseif(VOTCA_MODULE STREQUAL "xtp")
    set(VOTCA_DEPENDS ctp)
  endif()
  ExternalProject_Add(${VOTCA_MODULE}
   DEPENDS ${VOTCA_DEPENDS}
   SOURCE_DIR ${PROJECT_SOURCE_DIR}/${VOTCA_MODULE}
   CMAKE_ARGS 
     -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
     -DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
     -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
     -DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
     -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
     -DBUILD_SHARED_LIBS:BOOL=${BUILD_SHARED_LIBS}
     -DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_INSTALL_PREFIX}
     -DCMAKE_INSTALL_RPATH_USE_LINK_PATH:BOOL=${CMAKE_INSTALL_RPATH_USE_LINK_PATH}
  )
endforeach()
