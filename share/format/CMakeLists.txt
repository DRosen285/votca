find_package(LLVM QUIET)
set_package_properties(LLVM PROPERTIES TYPE OPTIONAL PURPOSE "Needed for automatic code formatting with clang-format")
if(LLVM_FOUND)
  find_package(Clang QUIET)
  set_package_properties(Clang PROPERTIES TYPE OPTIONAL PURPOSE "Needed for automatic code formatting with clang-format")
endif()
if(TARGET clang-format)
  set(FORMAT_SOURCES)
  foreach(DIR ${ENABLED_VOTCA_PACKAGES})
    set(ABS_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../../${DIR})
    file(GLOB_RECURSE ${DIR}_SOURCES ${ABS_DIR}/*.cc ${ABS_DIR}/*.h)
    list(APPEND FORMAT_SOURCES ${${DIR}_SOURCES})
  endforeach()
  add_custom_target(format
    COMMAND $<TARGET_FILE:clang-format> -i -style=file ${FORMAT_SOURCES}
    DEPENDS ${FORMAT_SOURCES})
endif()
