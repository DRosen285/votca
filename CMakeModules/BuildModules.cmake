include(ExternalProject)
set(ENABLED_VOTCA_PACKAGES tools csg csg-tutorials)
if(BUILD_CSG_MANUAL)
  list(APPEND ENABLED_VOTCA_PACKAGES csg-manual)
endif()
if(BUILD_CSGAPPS)
  list(APPEND ENABLED_VOTCA_PACKAGES csgapps)
endif()
if(BUILD_CTP)
  list(APPEND ENABLED_VOTCA_PACKAGES ctp)
endif()
if(BUILD_XTP)
  list(APPEND ENABLED_VOTCA_PACKAGES xtp)
endif()

include(GNUInstallDirs)
set(VOTCA_RPATH_OPTS)
# for manuals we need injected rpath to call binaries
if(BUILD_CSG_MANUAL OR BUILD_CSG_MANUAL OR BUILD_CSG_MANUAL)
  list(FIND CMAKE_PLATFORM_IMPLICIT_LINK_DIRECTORIES "${CMAKE_INSTALL_FULL_LIBDIR}" isSystemDir)
  if("${isSystemDir}" STREQUAL "-1")
    set(VOTCA_RPATH_OPTS "-DCMAKE_INSTALL_RPATH=${CMAKE_INSTALL_FULL_LIBDIR}")
  endif("${isSystemDir}" STREQUAL "-1")
endif()
set(VOTCA_TEST_OPTS 0)
if(ENABLE_TESTING)
  set(VOTCA_TEST_OPTS 1)
endif()
foreach(VOTCA_MODULE ${ENABLED_VOTCA_PACKAGES})
  set(VOTCA_DEPENDS)
  set(VOTCA_EXTRA_OPTS)
  if(VOTCA_MODULE STREQUAL "csg")
    set(VOTCA_DEPENDS tools)
  elseif((VOTCA_MODULE STREQUAL "csgapps") OR (VOTCA_MODULE STREQUAL "csg-manual"))
    set(VOTCA_DEPENDS csg)
  elseif(VOTCA_MODULE STREQUAL "csg-tutorials")
    set(VOTCA_DEPENDS csg)
    set(VOTCA_EXTRA_OPTS "-DVOTCA_PATH=${CMAKE_INSTALL_FULL_BINDIR}")
  elseif(VOTCA_MODULE STREQUAL "ctp")
    set(VOTCA_DEPENDS csg)
    if(BUILD_CTP_MANUAL)
      set(VOTCA_EXTRA_OPTS -DBUILD_CTP_MANUAL=ON)
    endif()
  elseif(VOTCA_MODULE STREQUAL "xtp")
    set(VOTCA_DEPENDS ctp)
    if(BUILD_XTP_MANUAL)
      set(VOTCA_EXTRA_OPTS -DBUILD_XTP_MANUAL=ON)
      endif()
  endif()
  ExternalProject_Add(${VOTCA_MODULE}
   DEPENDS ${VOTCA_DEPENDS}
   TEST_BEFORE_INSTALL ${VOTCA_TEST_OPTS}
   SOURCE_DIR ${PROJECT_SOURCE_DIR}/${VOTCA_MODULE}
   CMAKE_ARGS 
     -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
     -DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
     -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
     -DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
     -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
     -DBUILD_SHARED_LIBS:BOOL=${BUILD_SHARED_LIBS}
     -DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_INSTALL_PREFIX}
     -DCMAKE_INSTALL_RPATH_USE_LINK_PATH:BOOL=${CMAKE_INSTALL_RPATH_USE_LINK_PATH}
     -DENABLE_TESTING:BOOL=${ENABLE_TESTING}
     ${VOTCA_EXTRA_OPTS}
     ${VOTCA_RPATH_OPTS}
  )
endforeach()
